;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jedi Product
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jedi.product jazz


(import (jazz.io)
        (jazz.product))


;;;
;;;; Build
;;;


(definition (build descriptor (unit: unit #f) (force?: force? #f))
  (define (resources-dir)
    (let ((bundle (product-descriptor-build-bundle descriptor)))
      (if bundle
          (new-directory~ {Directory Build} (format "{a}.app/Contents/Resources" bundle))
        {Directory Build})))
  
  (let ((jedi-dir {Directory Jedi})
        (resources-dir (resources-dir)))
    (define (copy-info)
      (install-file '("README"))
      (install-file '("INSTALL")))
    
    (define (copy-fonts)
      (when (neq? kernel-platform 'windows)
        (install-directory '("jazz" "etc" "fonts"))))
    
    (define (copy-templates)
      (install-directory '("lib" "jedi" "templates")))
    
    (define (install-file path)
      (let ((src (new-file~ jedi-dir path))
            (dst (new-file~ resources-dir path)))
        (install-file~ src dst
          feedback: (lambda (action pathname)
                      (build-feedback "; {a} {a}..." action (get-name~ pathname))))))
    
    (define (install-directory path)
      (let ((src (new-directory~ jedi-dir path))
            (dst (new-directory~ resources-dir path)))
        (install-directory~ src dst
          feedback: (let ((first? #t))
                      (lambda (action pathname)
                        (when first?
                          (build-feedback "; {a} {a}..." action (get-name~ pathname))
                          (set! first? #f)))))))
    
    (build-product-descriptor descriptor unit: unit force?: force?)
    (initialize-aliases)
    (copy-info)
    (copy-fonts)
    (copy-templates)))


;;;
;;;; Install
;;;


(cond-expand
  (cocoa
    (definition (install descriptor)
      (initialize-aliases)
      (let ((bundle (product-descriptor-build-bundle descriptor)))
        (when bundle
          (let ((src-root {Directory Build})
                (dst-root (new-directory~ (get-parent~ (get-parent~ {Directory Jedi})) "releases"))
                (bundle-app (format "{a}.app" bundle)))
            (let ((src-app (new-directory~ src-root bundle-app))
                  (dst-app (new-directory~ dst-root bundle-app)))
              (let ((macos-dir (new-directory~ dst-app '("Contents" "MacOS")))
                    (libraries '("libcairo.2.dylib"
                                 "libfontconfig.dylib"
                                 "libfreetype.dylib"
                                 "libpixman-1.0.dylib"
                                 "libpng15.15.dylib")))
                ;; bundle
                (install-directory~ src-app dst-app
                  filter: (lambda (action pathname)
                            (let ((parent-dir (get-parent~ pathname)))
                              (cond ((pathname=? parent-dir macos-dir)
                                     (not (and (eq? action 'delete)
                                               (let ((filename (get-name~ pathname)))
                                                 (or (filename=? filename "lib")
                                                     (filename=? filename ".repository")
                                                     (member? filename libraries test: filename=?))))))
                                    (else
                                     #t))))
                  feedback: (lambda (action pathname)
                              (build-feedback "; {a} {a}..." action (get-name~ pathname))))
                ;; Repository
                (install-file~ (new-file~ src-root ".repository") (new-file~ macos-dir ".repository")
                  feedback: (lambda (action pathname)
                              (build-feedback "; {a} {a}..." action (get-name~ pathname))))
                ;; libraries
                (for-each (lambda (library)
                            (install-file~ (new-file~ src-root library) (new-file~ macos-dir library)
                              feedback: (lambda (action pathname)
                                          (build-feedback "; {a} {a}..." action (get-name~ pathname)))))
                          libraries)
                ;; lib
                (install-directory~ (new-directory~ src-root "lib") (new-directory~ macos-dir "lib")
                  filter: (lambda (action pathname)
                            (not (and (file? pathname)
                                      (member? (get-extension~ pathname) '("c" "m" "o" "dgs") test: extension=?))))
                  feedback: (lambda (action pathname)
                              (build-feedback "; {a} {a}..." action (get-name~ pathname)))))))))))
  (else
    (definition (install descriptor)
      )))


;;;
;;;; Register
;;;


(register-product 'jedi
  build: build
  install: install))
